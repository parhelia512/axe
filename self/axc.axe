// Author: Navid Momtahen (C) 2025
// License: GPL-3.0

use std.algorithms;
use std.io;
use std.os;
use std.lists;
use std.string;
use builds;
use gstate;

def main() {
    mut arena: Arena = Arena.create(1024);
    val args: ref StringList = get_cmdline_args(addr(arena));
    
    if args.len < 2 {
        println("Usage: axe <input.axe|axec> [options]");
        println("");
        println("Options:");
        println("  -o <name>     specify output binary name");
        println("  -e            keep the emitted file");
        println("  -r            run the built executable after compilation");
        println("  -tokens       print lexer tokens and exit");
        println("  -ast          print the parsed AST and exit");
        println("  -dll          build shared instead of standalone executable");
        println("  --release     build in release mode");
        println("  -l, --loud    loud debug output");
        println("  -I<path>      pass a C include directory");
        println("  --version, -v show axe version and exit\n");
        quit(0);
    }
    
    mut source_file: string = StringList.get(args, 1);
    mut other_arg: string = str("");
    mut output_filename: string = str("");

    if args.len > 2 {
        other_arg = strip(StringList.get(args, 2));
    }
    
    val inc_paths: ref StringList = StringList.create(addr(arena), 16);
    set_include_paths(inc_paths);
    
    mut i: i32 = 2;

    loop {
        if i >= args.len {
            break;
        }
        val arg: string = StringList.get(args, i);
        if equals_c(arg, "-o") and i + 1 < args.len {
            output_filename = StringList.get(args, i + 1);
            i = i + 2;
            continue;
        }
        if has_prefix(arg, str("-I")) and str_len(arg) > 2 {
            val inc_path: string = substring_se(arg, 2, str_len(arg));
            StringList.push(inc_paths, addr(arena), inc_path);
        }
        i = i + 1;
    }
    
    if strlst_contains_c(deref(args), "-v") or strlst_contains_c(deref(args), "--version") {
        println("Axe v0.0.1");
        println("Specification and compiler by Navid Momtahen ((C) GPL-3.0, 2025)\n");
        quit(0);
    }

    if strlst_contains_c(deref(args), "-l") or strlst_contains_c(deref(args), "--loud") {
        set_loud_logger(true);
    }

    if strlst_contains_c(deref(args), "--release") {
        set_release_build(true);
    }
    
    if strlst_contains_c(deref(args), "-e") {
        set_keep_emitted_file(true);
    }
    
    if strlst_contains_c(deref(args), "-r") {
        set_run_after_compile(true);
    }
    
    if strlst_contains_c(deref(args), "-tokens") {
        set_print_tokens(true);
    }
    
    if strlst_contains_c(deref(args), "-ast") {
        set_print_ast(true);
    }

    if !has_suffix(source_file, str(".axe")) and !has_suffix(source_file, str(".axec")) {
        source_file = concat(source_file, str(".axe"));
    }

    val is_axec: bool = has_suffix(source_file, str(".axec"));
    
    if !is_file(source_file) {
        println($"Error: File '{source_file}' not found");
        quit(1);
    }
    
    val ok: bool = compile_file(source_file, is_axec, output_filename);
    
    Arena.destroy(addr(arena));

    if !ok {
        quit(1);
    }

    quit(0);
}
