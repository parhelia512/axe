/// Author: Navid Momtahen (C) 2026
/// License: GPL-3.0
/// 
/// LLVM IR backend renderer for Axe compiler.

use ../../gstate;
use ../../structs (ASTNode);
use std.lists     (StringList);
use std.string;
use std.io;
use std.maps;
use std.arena;

/// Global LLVM IR string builder
mut g_llvm_sb: StringBuilder;

/// Track LLVM basic block counter
mut g_block_counter: i32;

/// Track LLVM register counter  
mut g_register_counter: i32;

/// Track variable to register mappings
mut g_var_registers: StringStringMap;

/// Track variable types
mut g_var_types: StringStringMap;

/// Track function declarations
mut g_function_decls: StringBoolMap;

/// Track function name prefixes (original -> prefixed)
mut g_function_prefixes: StringStringMap;

/// Track overload mappings (function_name -> type -> target_function)
mut g_overload_mappings: StringStringMap;

/// Track if LLVM renderer is initialized
mut g_llvm_initialized: bool = false;

/// Temporary storage for prefixes before initialization
mut g_temp_prefixes: list(string);

/// Track string constants counter
mut g_string_counter: i32;

/// Track string constants
mut g_string_constants: list(string);

/// Arena for LLVM renderer memory management
mut g_llvm_arena: Arena;

/// Initialize the LLVM renderer
pub def initialize_llvm_renderer() {
    g_llvm_arena = Arena.create(1024 * 1024);
    g_var_registers = deref(StringStringMap.create(addr(g_llvm_arena), 128));
    g_var_types = deref(StringStringMap.create(addr(g_llvm_arena), 128));
    g_function_decls = deref(StringBoolMap.create(addr(g_llvm_arena), 128));
    g_function_prefixes = deref(StringStringMap.create(addr(g_llvm_arena), 128));
    g_overload_mappings = deref(StringStringMap.create(addr(g_llvm_arena), 256));
    g_string_counter = 0;
    
    mut i: i32 = 0;
    loop {
        if i >= len(g_temp_prefixes) {
            break;
        }
        val pair: string = g_temp_prefixes.data[i];
        val colon_pos: i32 = find_char_from(pair, ':', cast[usize](0));
        if colon_pos > 0 {
            val original: string = substring_se(pair, 0, colon_pos);
            val prefixed: string = substring_se(pair, colon_pos + 1, cast[i32](str_len(pair)));
            StringStringMap.set(
                addr(g_function_prefixes),
                addr(g_llvm_arena),
                original,
                prefixed
            );
        }
        i++;
    }
    
    g_llvm_initialized = true;
}

/// Get next register name
def next_register(): string {
    val reg: string = concat(str("%"), i32_to_string(g_register_counter));
    g_register_counter++;
    return reg;
}

/// Get next block label
def next_block(): string {
    val block: string = concat(str("block"), i32_to_string(g_block_counter));
    g_block_counter++;
    return block;
}

/// Add a string constant to the global list
def add_string_constant(str_value: string) {
    append(g_string_constants, str_value);
    g_string_counter++;
}

/// Register a function prefix mapping (original -> prefixed) for LLVM backend
pub def llvm_renderer_add_prefix(original: string, prefixed: string) {
    if g_llvm_initialized {
        StringStringMap.set(addr(g_function_prefixes), addr(g_llvm_arena), original, prefixed);
    } else {
        val pair: string = concat(concat(original, str(":")), prefixed);
        append(g_temp_prefixes, pair);
    }
}

/// Analyze AST to find imported functions and create prefix mappings
def analyze_imports_for_prefixes(ast: ref ASTNode) {
    if ast == nil {
        return;
    }
    
    val node_type: string = ast.node_type;
    
    if equals_c(node_type, "Function") {
        val func_name: string = ast.data.function.name;
        val double_underscore_pos: i32 = find_substr(func_name, str("__"));
        if double_underscore_pos > 0 {
            val original_name: string = substring_se(func_name, double_underscore_pos + 2, cast[i32](str_len(func_name)));
            StringStringMap.set(addr(g_function_prefixes), addr(g_llvm_arena), original_name, func_name);
        }
    }
    
    if ast.children != nil {
        val children: ref list(ASTNode) = ast.children;
        val child_count: i32 = len(deref(children));
        mut i: i32 = 0;
        loop {
            if i >= child_count {
                break;
            }
            analyze_imports_for_prefixes(addr(children.data[i]));
            i++;
        }
    }
}

/// Generate LLVM IR for an AST node
pub def generate_llvm(ast: ref ASTNode): string {
    if ast == nil {
        return str("; Empty AST\n");
    }
    
    g_llvm_sb = StringBuilder.init(4096);
    g_block_counter = 0;
    g_register_counter = 1;
    
    val code: string = render_ast_node(ast);
    return code;
}

/// Render any AST node based on its type
def render_ast_node(ast: ref ASTNode): string {
    if ast == nil {
        return str("");
    }
    
    val node_type: string = ast.node_type;
    if str_len(node_type) == 0 {
        return str("");
    }
    
    if equals_c(node_type, "Program") {
        return render_program(ast);
    } elif equals_c(node_type, "Function") {
        return render_function(ast);
    } elif equals_c(node_type, "Return") {
        return render_return(ast);
    } elif equals_c(node_type, "Declaration") {
        return render_declaration(ast);
    } elif equals_c(node_type, "Assignment") {
        return render_assignment(ast);
    } elif equals_c(node_type, "FunctionCall") {
        return render_function_call(ast);
    } elif equals_c(node_type, "If") {
        return render_if(ast);
    } elif equals_c(node_type, "Loop") {
        return render_loop(ast);
    } elif equals_c(node_type, "Print") {
        return render_print(ast);
    } elif equals_c(node_type, "Overload") {
        return render_overload(ast);
    }
    
    return str("");
}

/// Render declaration node
def render_declaration(ast: ref ASTNode): string {
    val name: string = ast.data.declaration.name;
    val type_name: string = ast.data.declaration.type_name;
    val initializer: string = ast.data.declaration.initializer;
    
    val reg: string = next_register();
    StringStringMap.set(addr(g_var_registers), addr(g_llvm_arena), name, reg);
    StringStringMap.set(addr(g_var_types), addr(g_llvm_arena), name, type_name);
    
    mut sb: StringBuilder = StringBuilder.init(128);
    StringBuilder.append(addr(sb), str("  "));
    StringBuilder.append(addr(sb), reg);
    StringBuilder.append(addr(sb), str(" = alloca i32\n"));
    
    if str_len(initializer) > 0 {
        StringBuilder.append(addr(sb), str("  store i32 "));
        StringBuilder.append(addr(sb), initializer);
        StringBuilder.append(addr(sb), str(", i32* "));
        StringBuilder.append(addr(sb), reg);
        StringBuilder.append(addr(sb), str("\n"));
    }
    
    return StringBuilder.to_string(addr(sb));
}

/// Render assignment node
def render_assignment(ast: ref ASTNode): string {
    val variable: string = ast.data.assignment.variable;
    val expression: string = ast.data.assignment.expression;
    
    if !StringStringMap.contains(addr(g_var_registers), variable) {
        return str(""); 
    }
    
    val reg: string = StringStringMap.get(addr(g_var_registers), variable);
    mut sb: StringBuilder = StringBuilder.init(128);
    
    StringBuilder.append(addr(sb), str("  store i32 "));
    StringBuilder.append(addr(sb), expression);
    StringBuilder.append(addr(sb), str(", i32* "));
    StringBuilder.append(addr(sb), reg);
    StringBuilder.append(addr(sb), str("\n"));
    
    return StringBuilder.to_string(addr(sb));
}

/// Render function call node
def render_function_call(ast: ref ASTNode): string {
    val func_name_ast: string = ast.data.func_call.function_name;
    mut func_name: string = func_name_ast;
    val args: ref list(string) = ast.data.func_call.args;
    mut sb: StringBuilder = StringBuilder.init(256);
    
    if args != nil and len(deref(args)) > 0 {
        val first_arg: string = strip(args.data[0]);
        
        if StringStringMap.contains(addr(g_var_types), first_arg) {
            val arg_type: string = StringStringMap.get(addr(g_var_types), first_arg);
            val mapping_key: string = concat(concat(func_name_ast, str(":")), arg_type);
            
            if StringStringMap.contains(addr(g_overload_mappings), mapping_key) {
                val target_func: string = StringStringMap.get(
                    addr(g_overload_mappings),
                    mapping_key
                );
                func_name = target_func;
            }
        }
        
        val arg_list: list(string) = deref(args);
        mut i: i32 = 0;
        loop {
            if i >= len(arg_list) {
                break;
            }
            val arg: string = strip(arg_list.data[i]);
            
            if StringStringMap.contains(addr(g_var_registers), arg) {
                val reg: string = StringStringMap.get(addr(g_var_registers), arg);
                val load_reg: string = next_register();
                StringBuilder.append(addr(sb), str("  "));
                StringBuilder.append(addr(sb), load_reg);
                StringBuilder.append(addr(sb), str(" = load i32, i32* "));
                StringBuilder.append(addr(sb), reg);
                StringBuilder.append(addr(sb), str("\n"));
            }
            i++;
        }
    }
    
    if StringStringMap.contains(addr(g_function_prefixes), func_name) {
        func_name = StringStringMap.get(addr(g_function_prefixes), func_name);
    }
    
    StringBuilder.append(addr(sb), str("  call void @"));
    StringBuilder.append(addr(sb), func_name);
    StringBuilder.append(addr(sb), str("("));
    
    if args != nil and len(deref(args)) > 0 {
        val arg_list: list(string) = deref(args);
        mut i: i32 = 0;
        val load_reg_counter: i32 = g_register_counter - len(arg_list);
        loop {
            if i >= len(arg_list) {
                break;
            }
            if i > 0 {
                StringBuilder.append(addr(sb), str(", "));
            }
            val arg: string = strip(arg_list.data[i]);
            
            if StringStringMap.contains(addr(g_var_registers), arg) {
                val load_reg: string = concat(str("%"), i32_to_string(load_reg_counter + i));
                StringBuilder.append(addr(sb), str("i32 "));
                StringBuilder.append(addr(sb), load_reg);
            } else {
                StringBuilder.append(addr(sb), str("i32 "));
                StringBuilder.append(addr(sb), arg);
            }
            i++;
        }
    }
    
    StringBuilder.append(addr(sb), str(")\n"));
    
    return StringBuilder.to_string(addr(sb));
}

/// Render if statement
def render_if(ast: ref ASTNode): string {
    val condition: string = ast.data.if_node.condition;
    val then_label: string = next_block();
    val else_label: string = next_block();
    val end_label: string = next_block();
    mut sb: StringBuilder = StringBuilder.init(256);

    StringBuilder.append(addr(sb), str("  br i1 "));
    StringBuilder.append(addr(sb), condition);
    StringBuilder.append(addr(sb), str(", label %"));
    StringBuilder.append(addr(sb), then_label);
    StringBuilder.append(addr(sb), str(", label %"));
    StringBuilder.append(addr(sb), else_label);
    StringBuilder.append(addr(sb), str("\n\n"));
    StringBuilder.append(addr(sb), then_label);
    StringBuilder.append(addr(sb), str(":\n"));
    
    if ast.children != nil {
        val children: ref list(ASTNode) = ast.children;
        val child_count: i32 = len(deref(children));
        mut i: i32 = 0;
        
        loop {
            if i >= child_count {
                break;
            }
            val child_code: string = render_ast_node(addr(children.data[i]));
            StringBuilder.append(addr(sb), child_code);
            i++;
        }
    }
    
    StringBuilder.append(addr(sb), str("  br label %"));
    StringBuilder.append(addr(sb), end_label);
    StringBuilder.append(addr(sb), str("\n\n"));
    StringBuilder.append(addr(sb), else_label);
    StringBuilder.append(addr(sb), str(":\n"));
    StringBuilder.append(addr(sb), str("  br label %"));
    StringBuilder.append(addr(sb), end_label);
    StringBuilder.append(addr(sb), str("\n\n"));
    StringBuilder.append(addr(sb), end_label);
    StringBuilder.append(addr(sb), str(":\n"));
    
    return StringBuilder.to_string(addr(sb));
}

/// Render loop node
def render_loop(ast: ref ASTNode): string {
    val loop_label: string = next_block();
    val body_label: string = next_block();
    val end_label: string = next_block();
    
    mut sb: StringBuilder = StringBuilder.init(256);
    StringBuilder.append(addr(sb), str("  br label %"));
    StringBuilder.append(addr(sb), loop_label);
    StringBuilder.append(addr(sb), str("\n\n"));
    StringBuilder.append(addr(sb), loop_label);
    StringBuilder.append(addr(sb), str(":\n"));
    StringBuilder.append(addr(sb), str("  br label %"));
    StringBuilder.append(addr(sb), body_label);
    StringBuilder.append(addr(sb), str("\n\n"));
    StringBuilder.append(addr(sb), body_label);
    StringBuilder.append(addr(sb), str(":\n"));
    
    if ast.children != nil {
        val children: ref list(ASTNode) = ast.children;
        val child_count: i32 = len(deref(children));
        mut i: i32 = 0;
        
        loop {
            if i >= child_count {
                break;
            }
            val child_code: string = render_ast_node(addr(children.data[i]));
            StringBuilder.append(addr(sb), child_code);
            i++;
        }
    }
    
    StringBuilder.append(addr(sb), str("  br label %"));
    StringBuilder.append(addr(sb), loop_label);
    StringBuilder.append(addr(sb), str("\n\n"));
    StringBuilder.append(addr(sb), end_label);
    StringBuilder.append(addr(sb), str(":\n"));
    
    return StringBuilder.to_string(addr(sb));
}

/// Render program node
def render_program(ast: ref ASTNode): string {
    if ast.children == nil {
        return str("");
    }
    
    val children: ref list(ASTNode) = ast.children;
    val child_count: i32 = len(deref(children));
    mut i: i32 = 0;

    loop {
        if i >= child_count {
            break;
        }
        val child: ref ASTNode = addr(children.data[i]);
        analyze_imports_for_prefixes(child);
        i++;
    }
    
    i = 0;
    loop {
        if i >= child_count {
            break;
        }
        val child_code: string = render_ast_node(addr(children.data[i]));
        StringBuilder.append(addr(g_llvm_sb), child_code);
        i++;
    }
    
    mut final_sb: StringBuilder = StringBuilder.init(4096);
    
    if len(g_string_constants) > 0 {
        mut str_i: i32 = 0;
        loop {
            if str_i >= len(g_string_constants) {
                break;
            }
            StringBuilder.append(addr(final_sb), str("@.str"));
            StringBuilder.append(addr(final_sb), i32_to_string(str_i));
            StringBuilder.append(addr(final_sb), str(" = private unnamed_addr constant ["));
            StringBuilder.append(
                addr(final_sb),
                i32_to_string(str_len(g_string_constants.data[str_i]) + 1)
            );
            StringBuilder.append(addr(final_sb), str(" x i8] c\""));
            StringBuilder.append(addr(final_sb), g_string_constants.data[str_i]);
            StringBuilder.append(addr(final_sb), str("\\00\"\n"));
            str_i++;
        }
        StringBuilder.append(addr(final_sb), str("\n"));
    }
    
    if len(g_string_constants) > 0 {
        StringBuilder.append(addr(final_sb), str("declare i32 @puts(i8*)\n\n"));
    }
    
    StringBuilder.append(addr(final_sb), StringBuilder.to_string(addr(g_llvm_sb)));
    
    return StringBuilder.to_string(addr(final_sb));
}

/// Render function definition
def render_function(ast: ref ASTNode): string {
    val func_name: string = ast.data.function.name;
    val return_type: string = ast.data.function.return_type;
    val params: ref list(string) = ast.data.function.params;
    mut sb: StringBuilder = StringBuilder.init(512);
    
    StringBuilder.append(addr(sb), str("define "));
    if str_len(return_type) > 0 and equals_c(return_type, "void") {
        StringBuilder.append(addr(sb), str("void"));
    } else {
        StringBuilder.append(addr(sb), str("i32"));
    }
    StringBuilder.append(addr(sb), str(" @"));
    StringBuilder.append(addr(sb), func_name);
    StringBuilder.append(addr(sb), str("("));
    
    if params != nil and len(deref(params)) > 0 {
        val param_list: list(string) = deref(params);
        mut i: i32 = 0;
        loop {
            if i >= len(param_list) {
                break;
            }
            if i > 0 {
                StringBuilder.append(addr(sb), str(", "));
            }
            StringBuilder.append(addr(sb), str("i32"));
            i++;
        }
    }
    
    StringBuilder.append(addr(sb), str(") {\n"));
    StringBuilder.append(addr(sb), str("entry:\n"));
    
    mut has_return: bool = false;
    if ast.children != nil {
        val children: ref list(ASTNode) = ast.children;
        val child_count: i32 = len(deref(children));
        mut i: i32 = 0;
        
        loop {
            if i >= child_count {
                break;
            }
            val child: ref ASTNode = addr(children.data[i]);
            if equals_c(child.node_type, "Return") {
                has_return = true;
            }
            val child_code: string = render_ast_node(child);
            StringBuilder.append(addr(sb), child_code);
            i++;
        }
    }
    
    if !has_return {
        if str_len(return_type) > 0 and equals_c(return_type, "void") {
            StringBuilder.append(addr(sb), str("  ret void\n"));
        } else {
            StringBuilder.append(addr(sb), str("  ret i32 0\n"));
        }
    }
    
    StringBuilder.append(addr(sb), str("}\n\n"));
    return StringBuilder.to_string(addr(sb));
}

/// Render return statement
def render_return(ast: ref ASTNode): string {
    val expression: string = strip(ast.data.return_node.expression);
    
    mut sb: StringBuilder = StringBuilder.init(128);
    if str_len(expression) > 0 {
        if StringStringMap.contains(addr(g_var_registers), expression) {
            val reg: string = StringStringMap.get(addr(g_var_registers), expression);
            val load_reg: string = next_register();
            StringBuilder.append(addr(sb), str("  "));
            StringBuilder.append(addr(sb), load_reg);
            StringBuilder.append(addr(sb), str(" = load i32, i32* "));
            StringBuilder.append(addr(sb), reg);
            StringBuilder.append(addr(sb), str("\n"));
            StringBuilder.append(addr(sb), str("  ret i32 "));
            StringBuilder.append(addr(sb), load_reg);
            StringBuilder.append(addr(sb), str("\n"));
        } else {
            StringBuilder.append(addr(sb), str("  ret i32 "));
            StringBuilder.append(addr(sb), expression);
            StringBuilder.append(addr(sb), str("\n"));
        }
    } else {
        StringBuilder.append(addr(sb), str("  ret void\n"));
    }
    return StringBuilder.to_string(addr(sb));
}

/// Render print node (put statement)
def render_print(ast: ref ASTNode): string {
    val messages_ref: ref list(string) = ast.data.print.messages;
    val flags_ref: ref list(bool) = ast.data.print.is_expressions;

    if messages_ref == nil or flags_ref == nil {
        return str("");
    }

    val messages: list(string) = deref(messages_ref);
    val flags: list(bool) = deref(flags_ref);

    if len(messages) == 0 {
        return str("");
    }

    mut sb: StringBuilder = StringBuilder.init(256);
    
    if len(messages) == 1 and len(flags) == 1 and !flags.data[0] {
        val str_id: string = i32_to_string(g_string_counter);
        val call_reg: string = next_register();
        
        StringBuilder.append(addr(sb), str("  "));
        StringBuilder.append(addr(sb), call_reg);
        StringBuilder.append(addr(sb), str(" = call i32 @puts(i8* getelementptr inbounds (["));
        StringBuilder.append(addr(sb), i32_to_string(str_len(messages.data[0]) + 1));
        StringBuilder.append(addr(sb), str(" x i8], ["));
        StringBuilder.append(addr(sb), i32_to_string(str_len(messages.data[0]) + 1));
        StringBuilder.append(addr(sb), str(" x i8]* @.str"));
        StringBuilder.append(addr(sb), str_id);
        StringBuilder.append(addr(sb), str(", i32 0, i32 0))\n"));
        
        add_string_constant(messages.data[0]);
        
        return StringBuilder.to_string(addr(sb));
    }
    
    return StringBuilder.to_string(addr(sb));
}
/// Render overload node
def render_overload(ast: ref ASTNode): string {
    val name: string = ast.data.overload_node.name;
    val type_names_ref: ref list(string) = ast.data.overload_node.type_names;
    val targets_ref: ref list(string) = ast.data.overload_node.target_functions;

    if type_names_ref == nil or targets_ref == nil {
        return str("");
    }

    val type_names: list(string) = deref(type_names_ref);
    val targets: list(string) = deref(targets_ref);
    val count: i32 = len(type_names);
    
    if count == 0 or count != len(targets) {
        return str("");
    }

    mut i: i32 = 0;
    loop {
        if i >= count {
            break;
        }
        val type_name: string = type_names.data[i];
        val target_func: string = targets.data[i];
        val mapping_key: string = concat(concat(name, str(":")), type_name);
        StringStringMap.set(
            addr(g_overload_mappings),
            addr(g_llvm_arena),
            mapping_key,
            target_func
        );
        i++;
    }

    return str("");
}
