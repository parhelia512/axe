use std.io (
    print_i32,
    print_i64
);

/// Represents the current date and time
pub model DateTime {
    year: i32;
    month: i32;
    day: i32;
    hour: i32;
    minute: i32;
    second: i32;
    millisecond: i32;
    epoch_ms: i64;
}

platform posix {
    use external("time.h");
}

/// Suspends the current thread for the specified number of milliseconds
pub def wait(ms: i32) {
    platform posix {
        unsafe {
            C.sleep(ms / 1000);
        }
    }
    platform windows {
        unsafe {
            C.Sleep(ms);
        }
    }
}

/// Returns the current date and time
pub def now(): DateTime {
    platform posix {
        foreign { time_t, tm, timeval };
        unsafe {
            mut tv: $(timeval);
            C.gettimeofday(addr(tv), nil);
            mut epoch_ms: i64 = cast[i64](tv.tv_sec) * 1000 + cast[i64](tv.tv_usec) / 1000;
            mut t: time_t = tv.tv_sec;
            mut local_tm: ref $(tm) = C.localtime(addr(t));

            mut dt: DateTime;
            dt.year        = local_tm.tm_year + 1900;
            dt.month       = local_tm.tm_mon + 1;
            dt.day         = local_tm.tm_mday;
            dt.hour        = local_tm.tm_hour;
            dt.minute      = local_tm.tm_min;
            dt.second      = local_tm.tm_sec;
            dt.millisecond = cast[i32](tv.tv_usec / 1000);
            dt.epoch_ms    = epoch_ms;

            return dt;
        }
    }

    platform windows {
        foreign { SYSTEMTIME, FILETIME, ULARGE_INTEGER };
        unsafe {
            mut st: SYSTEMTIME;
            C.GetLocalTime(addr(st));

            mut ft: FILETIME;
            C.GetSystemTimeAsFileTime(addr(ft));

            mut uli: ULARGE_INTEGER;
            uli.LowPart = ft.dwLowDateTime;
            uli.HighPart = ft.dwHighDateTime;

            val win_time_100ns: i64 = uli.QuadPart;
            val unix_time_100ns: i64 = win_time_100ns - 116444736000000000;
            val epoch_ms: i64 = unix_time_100ns / 10000;

            mut dt: DateTime;
            dt.year        = st.wYear;
            dt.month       = st.wMonth;
            dt.day         = st.wDay;
            dt.hour        = st.wHour;
            dt.minute      = st.wMinute;
            dt.second      = st.wSecond;
            dt.millisecond = st.wMilliseconds;
            dt.epoch_ms    = epoch_ms;

            return dt;
        }
    }
}


/// Simple and accurate epoch-based difference calculation
pub def difference(dt1: DateTime, dt2: DateTime): i64 {
    return dt2.epoch_ms - dt1.epoch_ms;
}

/// Prints the date and time in the format "YYYY-MM-DD HH:MM:SS.mmm" with optional newline
pub def print_time(dt: DateTime, newline: bool) {
    print_i32(dt.year);
    print "-";
    print_i32(dt.month);
    print "-";
    print_i32(dt.day);
    print " ";
    print_i32(dt.hour);
    print ":";
    print_i32(dt.minute);
    print ":";
    print_i32(dt.second);
    print ".";
    print_i32(dt.millisecond);
    if newline {
        println "";
    }
}

test {
    print "Started waiting at:\t";
    print_time(now(), true);
    val start: DateTime = now();
    wait(1000);
    print "Done waiting at:\t";
    print_time(now(), true);
    println "Difference: ";
    print_i64(difference(start, now()));
}
