use std.string;

/// Print out some value with a newline
pub overload println(x: generic) {
    string       => println_str;
    ref char     => println_chrptr;
    val ref char => println_chrptr;
    i32          => println_i32;
    char         => println_char;
    f32          => println_f32;
    f64          => println_f64;
    i64          => println_i64;
    bool         => println_bool;
    usize        => println_i64;
}(x);

/// Print out some value with no newline
pub overload print(x: generic) {
    string       => print_str;
    ref char     => print_chrptr;
    val ref char => print_chrptr;
    i32          => print_i32;
    char         => print_char;
    f32          => print_f32;
    f64          => print_f64;
    i64          => print_i64;
    bool         => print_bool;
    usize        => print_i64;
}(x);

/// Print out some char
pub def print_char(value: char) {
    unsafe {
        C.printf("%c", value);
    }
}

/// Print out some char with a newline
pub def println_char(value: char) {
    unsafe {
        C.printf("%c\n", value);
    }
}

/// Print out some string
pub def print_chrptr(value: ref char) {
    unsafe {
        C.printf("%s", value);
    }
}

/// Print out some string with a newline
pub def println_chrptr(value: ref char) {
    unsafe {
        C.printf("%s\n", value);
    }
}

/// Print out some string
pub def print_str(value: string) {
    unsafe {
        C.printf("%s", value.data);
    }
}

/// Print out some string with a newline
pub def println_str(value: string) {
    unsafe {
        C.printf("%s\n", value.data);
    }
}

/// Print out some float with no newline
pub def print_f32(value: f32) {
    unsafe {
        C.printf("%.10f", value);
    }
}

/// Print a float with newline
pub def println_f32(value: f32) {
    unsafe {
        C.printf("%.10f\n", value);
    }
}

/// Print an f64 with newline
pub def println_f64(value: f64) {
    unsafe {
        C.printf("%.10f\n", value);
    }
}

/// Print an f64 with no newline
pub def print_f64(value: f64) {
    unsafe {
        C.printf("%.10f", value);
    }
}

/// Print some i64 with newline
pub def println_i64(value: i64) {
    unsafe {
        C.printf("%ld\n", value);
    }
}

/// Print some i64 with no newline
pub def print_i64(value: i64) {
    unsafe {
        C.printf("%ld", value);
    }
}

/// Print out some int with no newline
pub def print_i32(value: i32) {
    unsafe {
        C.printf("%d", value);
    }
}

/// Print an int with newline
pub def println_i32(value: i32) {
    unsafe {
        C.printf("%d\n", value);
    }
}

/// Print a boolean with no newline
pub def print_bool(value: bool) {
    unsafe {
        if value {
            C.printf("true");
        } else {
            C.printf("false");
        }
    }
}

/// Print a boolean with newline
pub def println_bool(value: bool) {
    unsafe {
        if value {
            C.printf("true\n");
        } else {
            C.printf("false\n");
        }
    }
}

/// Read an integer from stdin
pub def read_i32(): i32 {
    mut value: i32 = 0;
    unsafe {
        C.scanf("%d", addr(value));
    }
    return value;
}

/// Read a float from stdin
pub def read_f32(): f32 {
    mut value: f32 = 0.0;
    unsafe {
        C.scanf("%f", addr(value));
    }
    return value;
}

/// Read a string from stdin
pub def read_str(arena: Arena): string {
    val buffer_size: usize = 1024;
    mut buffer: ref char = Arena.alloc_array(addr(arena), C.sizeof(char), buffer_size);
    unsafe {
        C.scanf("%1023s", buffer);
    }
    return str(buffer);
}

/// Print to stderr with a newline
pub overload eprintln(x: generic) {
    string       => eprintln_str;
    ref char     => eprintln_chrptr;
    val ref char => eprintln_chrptr;
    i32          => eprintln_i32;
    char         => eprintln_char;
    f32          => eprintln_f32;
    f64          => eprintln_f64;
    i64          => eprintln_i64;
    bool         => eprintln_bool;
    usize        => eprintln_i64;
}(x);

/// Print to stderr with no newline
pub overload eprint(x: generic) {
    string       => eprint_str;
    ref char     => eprint_chrptr;
    val ref char => eprint_chrptr;
    i32          => eprint_i32;
    char         => eprint_char;
    f32          => eprint_f32;
    f64          => eprint_f64;
    i64          => eprint_i64;
    bool         => eprint_bool;
    usize        => eprint_i64;
}(x);

/// Print out some char to stderr
pub def eprint_char(value: char) {
    unsafe {
        C.fprintf(C.stderr, "%c", value);
    }
}

/// Print out some char to stderr with a newline
pub def eprintln_char(value: char) {
    unsafe {
        C.fprintf(C.stderr, "%c\n", value);
    }
}

/// Print out some string to stderr
pub def eprint_chrptr(value: ref char) {
    unsafe {
        C.fprintf(C.stderr, "%s", value);
    }
}

/// Print out some string to stderr with a newline
pub def eprintln_chrptr(value: ref char) {
    unsafe {
        C.fprintf(C.stderr, "%s\n", value);
    }
}

/// Print out some string to stderr
pub def eprint_str(value: string) {
    unsafe {
        C.fprintf(C.stderr, "%s", value.data);
    }
}

/// Print out some string to stderr with a newline
pub def eprintln_str(value: string) {
    unsafe {
        C.fprintf(C.stderr, "%s\n", value.data);
    }
}

/// Print out some float to stderr with no newline
pub def eprint_f32(value: f32) {
    unsafe {
        C.fprintf(C.stderr, "%.10f", value);
    }
}

/// Print a float to stderr with newline
pub def eprintln_f32(value: f32) {
    unsafe {
        C.fprintf(C.stderr, "%.10f\n", value);
    }
}

/// Print some f64 to stderr with newline
pub def eprintln_f64(value: f64) {
    unsafe {
        C.fprintf(C.stderr, "%.10f\n", value);
    }
}

/// Print some f64 to stderr with no newline
pub def eprint_f64(value: f64) {
    unsafe {
        C.fprintf(C.stderr, "%.10f", value);
    }
}

/// Print some i64 to stderr with newline
pub def eprintln_i64(value: i64) {
    unsafe {
        C.fprintf(C.stderr, "%ld\n", value);
    }
}

/// Print some i64 to stderr with no newline
pub def eprint_i64(value: i64) {
    unsafe {
        C.fprintf(C.stderr, "%ld", value);
    }
}

/// Print out some int to stderr with no newline
pub def eprint_i32(value: i32) {
    unsafe {
        C.fprintf(C.stderr, "%d", value);
    }
}

/// Print an int to stderr with newline
pub def eprintln_i32(value: i32) {
    unsafe {
        C.fprintf(C.stderr, "%d\n", value);
    }
}

/// Print a boolean to stderr with no newline
pub def eprint_bool(value: bool) {
    unsafe {
        if value {
            C.fprintf(C.stderr, "true");
        } else {
            C.fprintf(C.stderr, "false");
        }
    }
}

/// Print a boolean to stderr with newline
pub def eprintln_bool(value: bool) {
    unsafe {
        if value {
            C.fprintf(C.stderr, "true\n");
        } else {
            C.fprintf(C.stderr, "false\n");
        }
    }
}

platform windows {
    use external("conio.h");
    extern def _getch(): i32;
}

opaque { termios };

platform posix {
    use external("termios.h");
    use external("unistd.h");
    
    extern def tcgetattr(fd: i32, termios_p: ref termios): i32;
    extern def tcsetattr(fd: i32, optional_actions: i32, termios_p: ref termios): i32;
}

/// Read a single keypress from stdin without waiting for enter keypress
pub def read_key(): i32 {
    platform windows {
        unsafe {
            return _getch();
        }
    }
    platform posix {
        unsafe {
            mut oldt: $(termios);
            mut newt: $(termios);
            tcgetattr(0, addr(oldt));
            newt = oldt;
            
            foreign {ICANON, ECHO, TCSANOW};
            newt.c_lflag = newt.c_lflag & ~(ICANON | ECHO);

            tcsetattr(0, TCSANOW, addr(newt));
            val ch: i32 = C.getchar();
            tcsetattr(0, TCSANOW, addr(oldt));
            return ch;
        }
    }
}

test {
    val x: f32 = 3.14159;
    val y: f32 = 2.71828;

    print_f32(x);
    println "";
    println "Testing println_float:";
    println_f32(y);

    print_i32(42);
    println "";
    println "Testing println_int:";
    println_i32(42);

    print_bool(true);
    println "";
    println "Testing println_bool:";
    println_bool(true);

    println "Testing read_key:";
    val key: i32 = read_key();
    println_i32(key);

    eprintln("Something to stderr.");
}
