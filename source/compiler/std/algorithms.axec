use std.arena;
use std.io;
use std.string;
use std.lists;

/// Check if some list contains a value.
def list_contains[T, T2](lst: T, value: T2): bool {
    for mut i = 0; i < lst.len; i++ {
        when T2 is i32 and T is IntList {
            if lst.data[i] == cast[i32](value) {
                return true;
            }
        } 
        when T2 is string and T is StringList {
            if compare(lst.data[i], cast[string](value)) == 0 {
                return true;
            }
        }
        when T2 is float and T is FloatList {
            if lst.data[i] == cast[float](value) {
                return true;
            }
        }
        when T2 is bool and T is BoolList {
            if lst.data[i] == cast[bool](value) {
                return true;
            }
        }
    }
    return false;
}

/// Check if some StringList contains a value
def strlst_contains(lst: StringList, value: string): bool {
    for mut i = 0; i < lst.len; i++ {
        if compare(lst.data[i], value) == 0 {
            return true;
        }
    }
    return false;
}

/// Check if some StringList contains a value, but search by raw char pointer.
def strlst_contains_c(lst: StringList, value: ref char): bool {
    for mut i = 0; i < lst.len; i++ {
        if equals_c(lst.data[i], value) {
            return true;
        }
    }
    return false;
}

/// Check if some IntList contains an i32.
def intlst_contains(lst: IntList, vlue: i32): bool { 
    for mut i = 0; i < lst.len; i++ {
        if lst.data[i] == vlue {
            return true;
        }
    }
    return false;
}

test {
    val arena: Arena = Arena.create(1024 * 10);
    println "\nTest 1: StringList contains";
    
    val lst: ref StringList = StringList.create(addr(arena), 10);
    StringList.push(lst, addr(arena), str("hello"));
    StringList.push(lst, addr(arena), str("world"));
    
    assert strlst_contains(deref(lst), str("hello")), "Expected hello to be found in the list";
    assert !strlst_contains(deref(lst), str("goodbye")), "Expected goodbye not to be found in the list";

    println "\nTest 2: IntList contains";
    val ilst: ref IntList = IntList.create(addr(arena), 10);
    IntList.push(ilst, addr(arena), 42);
    IntList.push(ilst, addr(arena), 1337);

    assert intlst_contains(deref(ilst), 42), "Expected 42 to be found in the list";
    assert !intlst_contains(deref(ilst), 7), "Expected 7 not to be found in the list";

    println(list_contains[IntList, i32](deref(ilst), 1337));
}
