/// Author: Navid Momtahen (C) 2026
/// License: GPL-3.0

use std.algorithms;
use std.io;
use std.os;
use std.lists;
use std.string;
use builds;
use gstate;

def main() {
    val arena: Arena = Arena.create(1024);
    val args: ref StringList = get_cmdline_args(addr(arena));
    
    if args.len < 2 {
        print_help_and_exit();
    }
    
    mut source_file: string = StringList.get(args, 1);
    mut output_filename: string = str("");
    val inc_paths: ref StringList = StringList.create(addr(arena), 16);
    val lib_paths: ref StringList = StringList.create(addr(arena), 16);
    val link_libs: ref StringList = StringList.create(addr(arena), 16);

    set_include_paths(inc_paths);
    set_library_paths(lib_paths);
    set_link_libraries(link_libs);
    
    mut i: i32 = 2;

    loop {
        if i >= args.len {
            break;
        }
        val arg: string = StringList.get(args, i);
        if equals_c(arg, "-o") and i + 1 < args.len {
            output_filename = StringList.get(args, i + 1);
            i += 2;
            continue;
        }
        if equals_c(arg, "-c") {
            set_compile_only(true);
            i++;
            continue;
        }
        if equals_c(arg, "--target") and i + 1 < args.len {
            val target: string = StringList.get(args, i + 1);
            set_target_triple(target);
            i += 2;
            continue;
        }
        if equals_c(arg, "--bootstrap") {
            set_bootstrap_mode(true);
            i++;
            continue;
        }
        if equals_c(arg, "--nowin") {
            set_no_windows_header(true);
            i++;
            continue;
        }
        if equals_c(arg, "--sysroot") and i + 1 < args.len {
            val sysroot: string = StringList.get(args, i + 1);
            set_sysroot_path(sysroot);
            i += 2;
            continue;
        }
        if equals_c(arg, "--backend") and i + 1 < args.len {
            val backend: string = StringList.get(args, i + 1);
            set_backend_type(backend);
            i += 2;
            continue;
        }
        if has_prefix(arg, str("-I")) and str_len(arg) > 2 {
            val inc_path: string = substring_se(arg, 2, cast[i32](str_len(arg)));
            StringList.push(inc_paths, addr(arena), inc_path);
        }
        if has_prefix(arg, str("-L")) and str_len(arg) > 2 {
            val lib_path: string = substring_se(arg, 2, cast[i32](str_len(arg)));
            StringList.push(lib_paths, addr(arena), lib_path);
        }
        if has_prefix(arg, str("-l")) and str_len(arg) > 2 {
            val lib_name: string = substring_se(arg, 2, cast[i32](str_len(arg)));
            StringList.push(link_libs, addr(arena), lib_name);
        }
        i++;
    }
    
    if strlst_contains_c(deref(args), "-v") or strlst_contains_c(deref(args), "--version") {
        println("Axe v0.0.16");
        println("Specification and compiler by Navid Momtahen ((C) GPL-3.0, 2025 - 2026)\n");
        quit(0);
    }

    if strlst_contains_c(deref(args), "-l") or strlst_contains_c(deref(args), "--loud") {
        set_loud_logger(true);
    }

    if strlst_contains_c(deref(args), "-q") or strlst_contains_c(deref(args), "--quiet") {
        set_quiet_mode(true);
    }

    if strlst_contains_c(deref(args), "--release") {
        set_release_build(true);
    }
    
    if strlst_contains_c(deref(args), "-e") {
        set_keep_emitted_file(true);
    }
    
    if strlst_contains_c(deref(args), "-r") {
        set_run_after_compile(true);
    }
    
    if strlst_contains_c(deref(args), "--help") or strlst_contains_c(deref(args), "-h") {
        print_help_and_exit();
    }

    if strlst_contains_c(deref(args), "-tokens") {
        set_print_tokens(true);
    }
    
    if strlst_contains_c(deref(args), "-ast") {
        set_print_ast(true);
    }
    
    if strlst_contains_c(deref(args), "-dll") {
        set_build_shared_lib(true);
    }
    
    if strlst_contains_c(deref(args), "--syntax-check") {
        set_syntax_check_only(true);
        set_quiet_mode(true);
    }

    if !has_suffix(source_file, str(".axe")) and !has_suffix(source_file, str(".axe")) {
        source_file = concat(source_file, str(".axe"));
    }

    val is_axec: bool = has_suffix(source_file, str(".axe"));
    
    if !is_file(source_file) {
        println($"Error: File '{source_file}' not found");
        quit(1);
    }
    
    val ok: bool = compile_file(source_file, is_axec, output_filename);
    
    Arena.destroy(addr(arena));

    if !ok {
        quit(1);
    }

    quit(0);
}

/// Show help message and exit.
def print_help_and_exit() {
    println("Usage: axe <input.axe> [options]");
    println("");
    println("Options:");
    println("  -o <name>         Specify output binary name");
    println("  -c                Compile only; produce object file (.obj or .o)");
    println("  -e                Keep the emitted file");
    println("  -r                Run the built executable after compilation");
    println("  -tokens           Print lexer tokens and exit");
    println("  -ast              Print the parsed AST and exit");
    println("  -dll              Build shared instead of standalone executable");
    println("  --release         Build in release mode");
    println("  --backend <name>  Build either with the C or LLVM backend");
    println("  --bootstrap       Emit without line directives");
    println("  -l, --loud        Loud debug output");
    println("  -q, --quiet       Suppress all output");
    println("  -I<path>          Pass a C include directory");
    println("  -L<path>          Add C library search path");
    println("  -l<name>          Link against C library (e.g. -lm, -lSDL2)");
    platform windows {
        println("  --nowin           Do not include windows.h automatically");
    }
    println("  --syntax-check    Check syntax only (no code generation)");
    println("  --target <triple> Cross-compile for target (e.g. x86_64-w64-mingw32)");
    println("  --sysroot <path>  Specify sysroot for cross-compilation");
    println("  --version, -v     Show axe version and exit");
    println("  --help, -h        Show this help message and exit");
    println("");
    println("Note: Cross-compiling requires the target platform's SDK and sysroot.\n");
    quit(0);
}
