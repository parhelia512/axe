use std.io (
    print_i32,
    print_i64
);

/// Represents the current date and time
model DateTime {
    year: i32;
    month: i32;
    day: i32;
    hour: i32;
    minute: i32;
    second: i32;
    millisecond: i32;
    epoch_ms: i64;
}

/// Suspends the current thread for the specified number of milliseconds
def wait(ms: i32) {
    platform posix {
        unsafe {
            C.sleep(ms / 1000);
        }
    }
    platform windows {
        unsafe {
            C.Sleep(ms);
        }
    }
}

/// Returns the current date and time
def now(): DateTime {
    platform posix {
        raw {
            struct timeval tv;
            gettimeofday(&tv, NULL);            
            long long epoch_ms = (long long)tv.tv_sec * 1000 + (long long)tv.tv_usec / 1000;
            time_t t = tv.tv_sec;
            struct tm *tm = localtime(&t);
            std_time_DateTime dt;
            dt.year = tm->tm_year + 1900;
            dt.month = tm->tm_mon + 1;
            dt.day = tm->tm_mday;
            dt.hour = tm->tm_hour;
            dt.minute = tm->tm_min;
            dt.second = tm->tm_sec;
            dt.millisecond = (int)(tv.tv_usec / 1000);
            dt.epoch_ms = epoch_ms;
            return dt;
        }
    }
    platform windows {
        raw {
            SYSTEMTIME st;
            GetLocalTime(&st);
            FILETIME ft;
            GetSystemTimeAsFileTime(&ft);
            ULARGE_INTEGER uli;
            uli.LowPart = ft.dwLowDateTime;
            uli.HighPart = ft.dwHighDateTime;
            long long win_time_100ns = uli.QuadPart;
            long long unix_time_100ns = win_time_100ns - 116444736000000000LL;
            long long epoch_ms = unix_time_100ns / 10000;

            std_time_DateTime dt;
            dt.year = st.wYear;
            dt.month = st.wMonth;
            dt.day = st.wDay;
            dt.hour = st.wHour;
            dt.minute = st.wMinute;
            dt.second = st.wSecond;
            dt.millisecond = st.wMilliseconds;
            dt.epoch_ms = epoch_ms;
            return dt;
        }
    }
}

/// Simple and accurate epoch-based difference calculation
def difference(dt1: DateTime, dt2: DateTime): i64 {
    return dt2.epoch_ms - dt1.epoch_ms;
}

/// Prints the date and time in the format "YYYY-MM-DD HH:MM:SS.mmm" with optional newline
def print_time(dt: DateTime, newline: bool) {
    print_i32(dt.year);
    print "-";
    print_i32(dt.month);
    print "-";
    print_i32(dt.day);
    print " ";
    print_i32(dt.hour);
    print ":";
    print_i32(dt.minute);
    print ":";
    print_i32(dt.second);
    print ".";
    print_i32(dt.millisecond);
    if newline {
        println "";
    }
}

test {
    print "Started waiting at:\t";
    print_time(now(), true);
    val start: DateTime = now();
    wait(1000);
    print "Done waiting at:\t";
    print_time(now(), true);
    println "Difference: ";
    print_i64(difference(start, now()));
}
