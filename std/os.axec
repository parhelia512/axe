use std.string (
    string
);
use std.io (
    print_str
);
use std.errors (
    error, panic
);
use std.lists (
    StringList
);
use std.arena (
    Arena
);

/// Execute a shell command. Returns exit status.
pub def exec_from_string(cmd: string): i32 {
    mut val result: i32 = 0;
    unsafe {
        result = C.system(cmd.data);
    }
    return result;
}

/// Execute a shell command with raw C string.
/// Returns exit status.
pub def exec(cmd: char*): i32 {
    mut val result: i32 = 0;
    unsafe {
        result = C.system(cmd);
    }
    return result;
}

/// Quit with some code.
pub def quit(code: i32) {
    unsafe {
        C.exit(code);
    }
}

/// Check if a file exists.
pub def file_exists(path: string): bool {
    mut val out: bool = false;
    raw {
        FILE *f = fopen(path.data, "r");
        if (f != NULL) {
            fclose(f);
            out = true;
        }
    }
    return out;
}

/// Read entire file into a newly allocated string.
pub def read_file(path: string): string {
    mut val out: string;

    raw {
        FILE *f = fopen(path.data, "rb");
        if (!f) {
            std__errors__panic(std__errors__error_create("Failed to open file"));
        }

        fseek(f, 0, SEEK_END);
        long size = ftell(f);
        fseek(f, 0, SEEK_SET);

        char *buf = (char*)malloc(size + 1);
        fread(buf, 1, size, f);
        buf[size] = 0;
        fclose(f);
    }

    return string.create(buf);
}

/// Write a string to a file, replacing its contents.
/// Returns true on success.
pub def write_file(path: string, contents: string): bool {
    mut val ok: bool = false;

    raw {
        FILE *f = fopen(path.data, "wb");
        if (!f) {
            return false;
        }
        fwrite(contents.data, 1, contents.len, f);
        fclose(f);
        ok = true;
    }

    return ok;
}

use external("sys/stat.h");
use external("sys/types.h");

/// Determine if a path is a directory.
pub def is_directory(path: string): bool {
    mut val out: bool = false;

    platform posix {
        raw {
            struct stat sb;
            if (stat(path.data, &sb) == 0) {
                out = S_ISDIR(sb.st_mode);
            }
        }
    }

    platform windows {
        raw {
            struct _stat sb;
            if (_stat(path.data, &sb) == 0) {
                out = (sb.st_mode & _S_IFDIR) != 0;
            }
        }
    }

    return out;
}

/// Determine if a path is a regular file.
pub def is_file(path: string): bool {
    mut val out: bool = false;

    platform posix {
        raw {
            struct stat sb;
            if (stat(path.data, &sb) == 0) {
                out = S_ISREG(sb.st_mode);
            }
        }
    }

    platform windows {
        raw {
            struct _stat sb;
            if (_stat(path.data, &sb) == 0) {
                out = (sb.st_mode & _S_IFREG) != 0;
            }
        }
    }

    return out;
}

/// Determine if a path is a symbolic link.
/// This always returns false on Windows.
pub def is_symbolic_link(path: string): bool {
    mut val out: bool = false;

    platform posix {
        raw {
            struct stat sb;
            if (lstat(path.data, &sb) == 0) {
                out = S_ISLNK(sb.st_mode);
            }
        }
    }

    platform windows {
        return false;
    }
    return out;
}

/// Delete a file.
pub def delete_file(path: string): bool {
    mut val ok: bool = false;
    raw {
        if (remove(path.data) == 0) {
            ok = true;
        }
    }
    return ok;
}

/// Delete some directory. It must be empty.
pub def rm_dir(path: string): bool {
    mut val ok: bool = false;
    raw {
        if (rmdir(path.data) == 0) {
            ok = true;
        }
    }
    return ok;
}

platform windows {
    use external("io.h");
    use external("direct.h");
}

platform posix {
    use external("dirent.h");
}

platform posix {
    opaque { DIR, dirent, stat };
    
    extern def opendir(name: char*): usize;
    extern def readdir(dirp: usize): usize;
    extern def closedir(dirp: usize): i32;
    extern def stat(pathname: char*, statbuf: usize): i32;

    def dirent_d_name(entry: usize): char* {
        unsafe {
            val d: dirent* = entry;
            return d*.d_name;
        }
    }

    def stat_is_dir(st: usize): i32 {
        unsafe {
            val s: stat* = st;
            return S_ISDIR(s*.st_mode);
        }
    }
}

platform windows {
    opaque { _finddata_t };
    
    extern def _findfirst(filespec: char*, fileinfo: usize): isize;
    extern def _findnext(handle: isize, fileinfo: usize): i32;
    extern def _findclose(handle: isize): i32;

    def finddata_name(data: usize): char* {
        unsafe {
            val info: _finddata_t* = data;
            return info*.name;
        }
    }

    def finddata_is_dir(data: usize): i32 {
        unsafe {
            val info: _finddata_t* = data;
            return (info*.attrib & _A_SUBDIR) != 0;
        }
    }
}

extern def malloc(size: usize): usize;
extern def free(ptr: usize);
extern def strlen(s: char*): usize;
extern def strcmp(s1: char*, s2: char*): i32;
extern def snprintf(buf: char*, size: usize, format: char*, ...): i32;
extern def remove(pathname: char*): i32;
extern def rmdir(path: char*): i32;

/// Delete some directory, it does NOT have to be empty.
/// Use with caution.
pub def rm_dir_recursive(path: string): bool {
    mut val ok: bool = true;

    platform posix {
        val d: usize = opendir(path.data);
        val path_len: usize = strlen(path.data);
        mut val r: i32 = 0;

        if d != 0 {
            loop {
                val p: usize = readdir(d);
                if p == 0 {
                    break;
                }
                
                val name: char* = dirent_d_name(p);
                if strcmp(name, ".") == 0 {
                    continue;
                }
                if strcmp(name, "..") == 0 {
                    continue;
                }

                val len: usize = path_len + strlen(name) + 2;
                val buf: usize = malloc(len);
                if buf == 0 {
                    r = -1;
                    break;
                }
                
                snprintf(buf, len, "%s/%s", path.data, name);

                val statbuf: usize = malloc(256);
                if stat(buf, statbuf) == 0 {
                    if stat_is_dir(statbuf) != 0 {
                        val sub_path: string = string.create(buf);
                        if !rm_dir_recursive(sub_path) {
                            r = -1;
                        }
                    } else {
                        if remove(buf) != 0 {
                            r = -1;
                        }
                    }
                } else {
                    r = -1;
                }
                
                free(statbuf);
                free(buf);
                
                if r != 0 {
                    break;
                }
            }
            C.closedir(d);
        }
        
        if r == 0 {
            if rmdir(path.data) != 0 {
                r = -1;
            }
        }
        
        if r != 0 {
            ok = false;
        }
    }

    platform windows {
        mut val r: i32 = 0;
        val search_buf: usize = malloc(260);
        snprintf(search_buf, 260, "%s\\*", path.data);
        
        val fileinfo: usize = malloc(320);
        val handle: isize = _findfirst(search_buf, fileinfo);
        free(search_buf);
        
        if handle != -1 {
            loop {
                val name: char* = finddata_name(fileinfo);
                if strcmp(name, ".") != 0 {
                    if strcmp(name, "..") != 0 {
                        val sub_buf: usize = malloc(260);
                        snprintf(sub_buf, 260, "%s\\%s", path.data, name);
                        
                        if finddata_is_dir(fileinfo) != 0 {
                            val sub_path: string = string.create(sub_buf);
                            if !rm_dir_recursive(sub_path) {
                                r = -1;
                            }
                        } else {
                            if remove(sub_buf) != 0 {
                                r = -1;
                            }
                        }
                        
                        free(sub_buf);
                    }
                }
                
                if r != 0 {
                    break;
                }
                
                if _findnext(handle, fileinfo) != 0 {
                    break;
                }
            }
            
            _findclose(handle);
        }
        
        free(fileinfo);
        
        if r == 0 {
            if rmdir(path.data) != 0 {
                r = -1;
            }
        }

        if r != 0 {
            ok = false;
        }
    }

    return ok;
}

platform posix {
    def stat_is_reg(st: usize): i32 {
        unsafe {
            val s: stat* = st;
            return S_ISREG(s*.st_mode);
        }
    }
}

/// Internal helper to collect files recursively into a StringList.
pub def collect_files_recursive(path: string, result: ref StringList, arena: ref Arena) {
    platform posix {
        val d: usize = opendir(path.data);
        if d == 0 {
            return;
        }

        loop {
            val p: usize = readdir(d);
            if p == 0 {
                break;
            }
            
            val name: char* = dirent_d_name(p);
            if strcmp(name, ".") == 0 {
                continue;
            }
            if strcmp(name, "..") == 0 {
                continue;
            }

            val len: usize = strlen(path.data) + strlen(name) + 2;
            val buf: usize = malloc(len);
            if buf == 0 {
                continue;
            }

            snprintf(buf, len, "%s/%s", path.data, name);

            val statbuf: usize = malloc(256);
            if stat(buf, statbuf) == 0 {
                if stat_is_dir(statbuf) != 0 {
                    val sub_path: string = string.create((char*)buf);
                    collect_files_recursive(sub_path, result, arena);
                } elif stat_is_reg(statbuf) != 0 {
                    val file_path: string = string.create((char*)buf);
                    StringList.push(result, arena, file_path);
                }
            }

            free(statbuf);
            free(buf);
        }

        C.closedir(d);
    }

    platform windows {
        val search_buf: usize = malloc(260);
        snprintf(search_buf, 260, "%s\\*", path.data);

        val fileinfo: usize = malloc(320);
        val handle: isize = _findfirst(search_buf, fileinfo);
        free(search_buf);

        if handle == -1 {
            free(fileinfo);
            return;
        }

        loop {
            val name: char* = finddata_name(fileinfo);
            if strcmp(name, ".") != 0 {
                if strcmp(name, "..") != 0 {
                    val sub_buf: usize = malloc(260);
                    snprintf(sub_buf, 260, "%s\\%s", path.data, name);

                    if finddata_is_dir(fileinfo) != 0 {
                        val sub_path: string = string.create(sub_buf);
                        collect_files_recursive(sub_path, result, arena);
                    } else {
                        val file_path: string = string.create(sub_buf);
                        StringList.push(result, arena, file_path);
                    }

                    free(sub_buf);
                }
            }

            if _findnext(handle, fileinfo) != 0 {
                break;
            }
        }

        _findclose(handle);
        free(fileinfo);
    }
}

/// List all regular files recursively under `path`.
pub def list_files_recursive(path: string, arena: ref Arena): ref StringList {
    mut val result: ref StringList;
    result = StringList.create(arena, 16);
    collect_files_recursive(path, result, arena);
    return result;
}

/// Get the user's home directory
pub def get_user_home_dir(): string {
    mut out: string = str("");
    unsafe {
        mut value: ref char = C.getenv("USERPROFILE");
        if !value {
            value = C.getenv("HOME");
        }
        if value {
            out.data = value;
        }
    }
    return out;
}

/// Get commandline args
pub def get_cmdline_args(arena: ref Arena): ref StringList {
    mut val result: ref StringList;
    result = StringList.create(arena, 10);
    
    raw {
        int argc = 0;
        char **argv = (char**)__axe_argv;
        while (argv[argc] != NULL) {
            argc++;
        }
        for (int i = 0; i < argc; i++) {
            struct std__string__string str = std__string__string_create(argv[i]);
            std__lists__StringList_push((struct std__lists__StringList*)result, arena, str);
        }
    }
    
    return result;
}

pub def get_cwd(): string {
    mut out: string;
    mut buffer: char[2048];

    platform posix {
        if getcwd(buffer, sizeof(buffer)) != nil {
            out = str(buffer);            
        } else {
            panic(error.create("Failed to get current working directory"));
        }
    }
    platform windows {
        if _getcwd(buffer, sizeof(buffer)) != nil {
            out = str(buffer);            
        } else {
            panic(error.create("Failed to get current working directory"));
        }
    }

    return out;
}

test {
    assert(exec("echo test") == 0, "exec should run successfully");
    assert(write_file(string.create("testfile.txt"), string.create("hello")), "write_file should succeed");
    assert(file_exists(string.create("testfile.txt")), "file should exist after writing");
    assert(is_file(string.create("testfile.txt")), "testfile.txt should be a file");
    assert(is_directory(string.create(".")), "current directory should be a directory");
    assert(!is_symbolic_link(string.create("testfile.txt")), "testfile.txt should not be a symbolic link");
    assert(delete_file(string.create("testfile.txt")), "delete_file should succeed");
    assert(!file_exists(string.create("testfile.txt")), "file should not exist after deletion");

    mut val arena: Arena = Arena.create(65536);
    val args: ref StringList = get_cmdline_args(addr(arena));
    assert(args.len >= 1, "there should be at least one commandline argument");

    print "(And that commandline argument is: ";
    
    for arg in args {
        print_str(arg);
    }

    print ")\n";

    println get_cwd();
}
